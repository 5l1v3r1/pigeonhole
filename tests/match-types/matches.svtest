require "vnd.dovecot.testsuite";

test_set "message" text:
From: stephan+sieve@drunksnipers.com
To: sirius@rename-it.nl
To: nico@vestingbar.nl
Cc: me@example.com
Cc: timo@dovecot.com
X-Hufter: TRUE
Subject: make your money very fast!!!
X-Spam-Score: **********
X-Bullshit: 33333???a
Message-ID: <90a02fe01fc25e131d0e9c4c45975894@example.com>
Comment:                                            
X-Subject: Log for successful build of Dovecot.

Het werkt!
.
;

/*
 * General conformance testing
 */

test "Empty string" {
	if not header :matches "comment" "" {
		test_fail "failed to match \"\" against \"\"";
	}

	if not header :matches "comment" "*" {
		test_fail "failed to match \"\" against \"*\"";
	}

	if header :matches "comment" "?" {
		test_fail "inappropriately matched \"\" against \"?\"";
	}
}

test "Multiple '*'" {
	if not address :matches "from" "*@d*ksn*ers.com" {
		test_fail "should have matched";
	}

	if address :matches "from" "*@d*kn*ers.com" {
		test_fail "should not have matched";
	}
}

test "End '*'" {
	if not address :matches "from" "stephan+sieve@drunksnipers.*" {
		test_fail "should have matched";
	}

	if address :matches "from" "stepan+sieve@drunksnipers.*" {
		test_fail "should not have matched";
	}
}

test "Begin '*'" {
	if not address :matches "from" "*+sieve@drunksnipers.com" {
		test_fail "should have matched";
	}

	if address :matches "from" "*+sieve@drunksnipers.om" {
		test_fail "should not have matched";
	}
}

test "Middle '?'" {
	if not address :matches "from" "stephan+sieve?drunksnipers.com" {
		test_fail "should have matched";
	}

	if address :matches "from" "stephan+sieve?drunksipers.com" {
		test_fail "should not have matched";
	}
}

test "Begin '?'" {
	if not address :matches "from" "?tephan+sieve@drunksnipers.com" {
		test_fail "should have matched";
	}

	if address :matches "from" "?tephan+sievedrunksnipers.com" {
		test_fail "should not have matched";
	}
}

test "End '?'" {
	if not address :matches "from" "stephan+sieve@drunksnipers.co?" {
		test_fail "should have matched";
	}

	if address :matches "from" "sephan+sieve@drunksnipers.co?" {
		test_fail "should not have matched";
	}
}

test "Multiple '?'" {
	if not address :matches "from" "?t?phan?sieve?drunksnip?rs.co?" {
		test_fail "should have matched";
	}

	if address :matches "from" "?t?phan?sieve?dunksnip?rs.co?" {
		test_fail "should not have matched";
	}
}

test "Escaped '?'" {
	if not header :matches "x-bullshit" "33333\\?\\?\\??" {
		test_fail "should have matched";
	}

	if header :matches "x-bullshit" "33333\\?\\?\\?" {
		test_fail "should not have matched";
	}
}

test "Escaped '?' following '*'" {
	if not header :matches "x-bullshit" "33333*\\?\\??" {
		test_fail "should have matched";
	}

}

test "Escaped '?' directly following initial '*'" {
	if not header :matches "X-Bullshit" "*\\?\\?\\?a" {
		test_fail "should have matched";
	}
}

test "Escaped '?' following initial '*'" {
	if not header :matches "x-bullshit" "*3333\\?\\?\\?a" {
		test_fail "should have matched";
	}
}

test "Escaped '*' with active '*' at the end" {
	if not header :matches "x-spam-score" "\\*\\*\\*\\*\\**" {
		test_fail "should have matched";
	}
}

test "All escaped '*'" {
	if not header :matches "x-spam-score" "\\*\\*\\*\\*\\*\\*\\*\\*\\*\\*" {
		test_fail "should have matched";
	}

	if header :matches "x-spam-score" "\\*\\*\\*\\*\\*\\*\\*\\*\\*\\*\\*" {
		test_fail "should not have matched";
	}
}

test "Middle not escaped '*'" {
	if not header :matches "x-spam-score" "\\*\\*\\***\\*\\*" {
		test_fail "should have matched";
	}
}

test "Escaped '*' alternating with '?'" {
	if not header :matches "x-spam-score" "\\*?\\*?\\*?\\*?\\*?" {
		test_fail "should have matched";
	}

	if header :matches "x-spam-score" "\\*?\\*?\\*?\\*?\\*??" {
		test_fail "should not have matched";
	}
}

test "All escaped" {
	if header :matches "x-bullshit" "\\*3333\\?\\?\\?a" {
		test_fail "should not have matched";
	}


	if header :matches "x-bullshit" "33333\\?\\?\\?aa" {
		test_fail "should not have matched";
	}

	if header :matches "x-bullshit" "\\f3333\\?\\?\\?a" {
		test_fail "should not have matched";
	}
}

test "Put '*' directly before '?'" {
	if header :matches "x-subject" "Log for *??????????? build of *" {
		test_fail "should not have matched";
	}

	if not header :matches "x-subject" "Log for *? build of *" {
		test_fail "should have matched";
	}
}

test "Put '?' directly before '*'" {
	if header :matches "x-subject" "Log for ???????????* build of *" {
		test_fail "should not have matched";
	}

	if not header :matches "x-subject" "Log for ?* build of *" {
		test_fail "should have matched";
	}
}


