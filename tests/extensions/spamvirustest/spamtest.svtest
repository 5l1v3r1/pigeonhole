require "vnd.dovecot.testsuite";
require "spamtest";
require "relational";
require "comparator-i;ascii-numeric";
require "variables";

/*
 * Value
 */

test_set "message" text:
From: legitimate@example.com
To: victim@dovecot.org
Subject: Not spammish
X-SpamCheck: No, score=-1.6 required=5.0 autolearn=no version=3.2.5
X-SpamCheck1: No, score=0.0 required=5.0 autolearn=no version=3.2.5
X-SpamCheck2: No, score=1.0 required=5.0 autolearn=no version=3.2.5
X-SpamCheck3: No, score=4.0 required=5.0 autolearn=no version=3.2.5
X-SpamCheck4: Yes, score=5.0 required=5.0 autolearn=no version=3.2.5
X-SpamCheck5: Yes, score=7.6 required=5.0 autolearn=no version=3.2.5

Test!
.
;

test_config :set "sieve_spamtest_status_header"
	"X-SpamCheck:[ \\ta-zA-Z]+, score=(-?[0-9]+.[0-9]+)";
test_config :set "sieve_spamtest_max_header"
	"X-SpamCheck:[ \\ta-zA-Z]+, score=-?[0-9]+.[0-9]+ required=(-?[0-9]+.[0-9]+)";
test_config :set "sieve_spamtest_status_type" "value";
test_config :reload "spamtest";

test "Value: subzero" {
	if spamtest :is "0" {
		test_fail "spamtest not configured or test failed";
	}

	if not spamtest :is "1" {
		if spamtest :matches "*" { }
		test_fail "wrong spam value produced: ${1}";
	}

	if spamtest :is "2" {
		test_fail "spam test matches anything";
	}
}

test_config :set "sieve_spamtest_status_header"
	"X-SpamCheck1:[ \\ta-zA-Z]+, score=(-?[0-9]+.[0-9]+)";
test_config :reload "spamtest";

test "Value: zero" {
	if spamtest :is "0" {
		test_fail "spamtest not configured or test failed";
	}

	if not spamtest :is "1" {
		if spamtest :matches "*" { }
		test_fail "wrong spam value produced: ${1}";
	}

	if spamtest :is "2" {
		test_fail "spam test matches anything";
	}
}

test_config :set "sieve_spamtest_status_header"
	"X-SpamCheck2:[ \\ta-zA-Z]+, score=(-?[0-9]+.[0-9]+)";
test_config :reload "spamtest";

test "Value: low" {
	if spamtest :is "0" {
		test_fail "spamtest not configured or test failed";
	}

	if not spamtest :value "gt" "1" {
		test_fail "too small spam value produced";
	}

	if not spamtest :value "eq" "2" {
		if spamtest :matches "*" { }
		test_fail "wrong spam value produced: ${1}";
	}
}

test_config :set "sieve_spamtest_status_header"
	"X-SpamCheck3:[ \\ta-zA-Z]+, score=(-?[0-9]+.[0-9]+)";
test_config :reload "spamtest";

test "Value: high" {
	if spamtest :is "0" {
		test_fail "spamtest not configured or test failed";
	}

	if not spamtest :value "eq" "8" {
		if spamtest :matches "*" { }
		test_fail "wrong spam value produced: ${1}";
	}
}

test_config :set "sieve_spamtest_status_header"
	"X-SpamCheck4:[ \\ta-zA-Z]+, score=(-?[0-9]+.[0-9]+)";
test_config :reload "spamtest";

test "Value: max" {
	if spamtest :is "0" {
		test_fail "spamtest not configured or test failed";
	}

	if not spamtest :value "eq" "10" {
		if spamtest :matches "*" { }
		test_fail "wrong spam value produced: ${1}";
	}
}

test_config :set "sieve_spamtest_status_header"
	"X-SpamCheck5:[ \\ta-zA-Z]+, score=(-?[0-9]+.[0-9]+)";
test_config :reload "spamtest";

test "Value: past-max" {
	if spamtest :is "0" {
		test_fail "spamtest not configured or test failed";
	}

	if not spamtest :value "eq" "10" {
		if spamtest :matches "*" { }
		test_fail "wrong spam value produced: ${1}";
	}
}

