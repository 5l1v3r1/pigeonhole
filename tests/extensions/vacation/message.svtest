require "vnd.dovecot.testsuite";
require "vacation";

test_set "message" text:
From: stephan@rename-it.nl
Subject: frop
References: <1234@local.machine.example> <3456@example.net>
 <435444@ttms.com> <4223@froop.nl> <m345444444@message-id.exp>
Message-ID: <432df324@rename-it.nl>
To: nico@vestingbar.nl

Frop
.
;

test "References" {
	vacation "I am not in today!";

	if not test_result_execute {
		test_fail "execution of result failed";
	}

	test_message :smtp 0;

	if not header :contains "references" "432df324@rename-it.nl" {
		test_fail "references header does not contain new id";
	}

	if anyof ( 
		not header :contains "references" "1234@local.machine.example", 
		not header :contains "references" "3456@example.net",
		not header :contains "references" "435444@ttms.com", 
		not header :contains "references" "4223@froop.nl", 
		not header :contains "references" "m345444444@message-id.exp" 
		) {
		test_fail "references header does not contain all existing ids";
	}

	if header :contains "references" "hutsefluts" {
		test_fail "references header contains nonsense";
	}
}

test "In-Reply-To" {
	if not header :is "in-reply-to" "<432df324@rename-it.nl>" {
		test_fail "in-reply-to header set incorrectly";
	}
}
