require "vnd.dovecot.testsuite";
require "envelope";
require "vacation";

test_set "message" text:
From: stephan@rename-it.nl
To: tss@iki.fi
Subject: Frop!

Frop!
.
;

test_set "envelope.from" "sirius@rename-it.nl";
test_set "envelope.to" "timo@iki.fi";

test "Basic" {
	vacation :addresses "tss@iki.fi" :from "Timo Sirainen <tss@iki.fi>" "I am gone";

	if not test_result_execute {
        test_fail "failed to execute vacation";
    }

    test_message :smtp 0;

    if not address :is "to" "sirius@rename-it.nl" {
        test_fail "to address incorrect";
    }

    if not address :is "from" "tss@iki.fi" {
        test_fail "from address incorrect";
    }

	if not envelope :is "to" "sirius@rename-it.nl" {
		test_fail "envelope recipient incorrect";
	}

	if not envelope :is "from" "" {
		test_fail "envelope sender not null";
	}
}

test_result_reset;
test_set "envelope.from" "<>";

test "Null Sender" {
	vacation :addresses "tss@iki.fi" "I am gone";

	if not test_result_execute {
		test_fail "failed to execute vacation";
	}

	if test_message :smtp 0 {
		test_fail "reject sent message to NULL sender";
	}
}
