require "vnd.dovecot.testsuite";
require "envelope";
require "subaddress";

test_set "message" text:
From: stephan+sieve@renane-it.nl
To: test+failed@example.com
Subject: subaddress test

Test!
.
;

test_set "envelope.to" "friep+frop@dovecot.org";
test_set "envelope.from" "list+request@lists.dovecot.org";

test "USER-message-from" {
	if not address :is :user "from" "stephan" {
		test_fail "wrong user part extracted";
	}

	if address :is :user "from" "nonsence" {
		test_fail "address test failed";
	}
}

test "DETAIL-message-from" {
	if not address :is :detail "from" "sieve" {
		test_fail "wrong user part extracted";
	}

	if address :is :detail "from" "nonsence" {
		test_fail "address test failed";
	}
}

test "USER-message-to" {
	if not address :contains :user "to" "est" {
		test_fail "wrong user part extracted";
	}

	if address :contains :user "to" "ail" {
		test_fail "address test failed";
	}
}

test "DETAIL-message-to" {
	if not address :contains :detail "to" "fai" {
		test_fail "wrong user part extracted";
	}

	if address :contains :detail "to" "sen" {
		test_fail "address test failed";
	}
}


test "ENVELOPE-to" {
    if not envelope :is :user "to" "friep" {
        test_fail "wrong user part extracted 1";
    }

    if not envelope :comparator "i;ascii-casemap" :is :user "to" "FRIEP" {
        test_fail "wrong user part extracted";
    }

    if envelope :comparator "i;ascii-casemap" :is :user "to" "FROP" {
        test_fail "envelope test failed";
    }
}

test "ENVELOPE-from" {
    if not envelope :comparator "i;ascii-casemap" :contains :detail "from" "QUES" {
        test_fail "wrong user part extracted";
    }

    if envelope :comparator "i;ascii-casemap" :contains :detail "from" "LIS" {
        test_fail "address test failed";
    }
}
