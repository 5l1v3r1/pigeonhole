require "vnd.dovecot.testsuite";
require "enotify";
require "relational";
require "envelope";
require "comparator-i;ascii-numeric";

/*
 * Simple test
 */

test_set "message" text:
From: stephan@rename-it.nl
To: nico@vestingbar.nl
Subject: Frop!

Klutsefluts.
.
;

test "Simple" {
	notify "mailto:stephan@rename-it.nl";

	if not test_result_execute {
		test_fail "failed to execute notify";
	}

	test_message :smtp 0;

	if not header :matches "Auto-Submitted" "auto-notified*" {
		test_fail "auto-submitted header set inappropriately";
	}

	if not exists "X-Sieve" {
		test_fail "x-sieve header missing from outgoing message";
	}
}

/*
 * Multiple recipients
 */

test_result_reset;

test_set "message" text:
From: stephan@rename-it.nl
To: nico@vestingbar.nl
Subject: Frop!

Klutsefluts.
.
;

test "Multiple recipients" {
	notify "mailto:timo@example.com%2cstephan@dovecot.org?cc=postmaster@vestingbar.nl&subject=Frop%20received";

	if not test_result_execute {
		test_fail "failed to execute notify";
	}

	test_message :smtp 0;

	if not address :is "to" "timo@example.com" {
		test_fail "first To address missing";
	}

	if not address :is "to" "stephan@dovecot.org" {
		test_fail "second To address missing";
	}

	if not address :is "cc" "postmaster@vestingbar.nl" {
		test_fail "first Cc address missing";
	}

	if not address :count "eq" :comparator "i;ascii-numeric" "to" "2" {
		test_fail "too many recipients in To header";
	} 

	if not address :count "eq" :comparator "i;ascii-numeric" "cc" "1" {
		test_fail "too many recipients in Cc header";
	} 

	if not header "subject" "Frop received" {
		test_fail "subject header set incorrectly";
	}

	test_message :smtp 1;

	if not header :matches "Auto-Submitted" "auto-notified*" {
		test_fail "auto-submitted header not found for second message";
	}

	test_message :smtp 2;

	if not header :matches "Auto-Submitted" "auto-notified*" {
		test_fail "auto-submitted header not found for third message";
	}
}

/*
 * Duplicate recipients
 */

test_result_reset;

test_set "message" text:
From: stephan@rename-it.nl
To: nico@vestingbar.nl
Subject: Frop!

Klutsefluts.
.
;

test "Duplicate recipients" {
	notify "mailto:timo@example.com%2cstephan@dovecot.org?cc=stephan@dovecot.org";
	notify "mailto:stephan@rename-it.nl?cc=timo@example.com";

	if not test_result_execute {
		test_fail "failed to execute notify";
	}

	test_message :smtp 0;

	if address "Cc" "stephan@dovecot.org" {
		test_fail "duplicate recipient not removed from first message";
	}

	test_message :smtp 1;

	if address "Cc" "timo@example.com" {
		test_fail "duplicate recipient not removed from second message";
	}
}


/*
 * Notifying on automated messages
 */

test_result_reset;

test_set "message" text:
From: stephan@rename-it.nl
To: nico@vestingbar.nl
Auto-submitted: auto-notify
Subject: Frop!

Klutsefluts.
.
;

test "Notifying on automated messages" {
	notify "mailto:stephan@rename-it.nl?cc=timo@example.com";

	if not test_result_execute {
		test_fail "failed to execute notify";
	}

	if test_message :smtp 0 {
		test_fail "notified of auto-submitted message";
	}
}

/*
 * Envelope
 */

test_set "message" text:
From: stephan@rename-it.nl
To: nico@vestingbar.nl
Subject: Frop!

Klutsefluts.
.
;

test_result_reset;

test_set "envelope.from" "sirius@rename-it.nl";
test_set "envelope.to" "bertus@vestingbar.nl";

test "Envelope" {
	notify "mailto:stephan@rename-it.nl?cc=timo@example.com";

	if not test_result_execute {
		test_fail "failed to execute notify";
	}

	test_message :smtp 0;

	if not envelope :localpart :is "from" "postmaster" {
		test_fail "envelope sender set incorrectly";
	}

	if not envelope :is "to" "stephan@rename-it.nl" {
		test_fail "envelope sender set incorrectly";
	}

	test_message :smtp 1;

	if not envelope :localpart :is "from" "postmaster" {
		test_fail "envelope sender set incorrectly";
	}

	if not envelope :is "to" "timo@example.com" {
		test_fail "envelope sender set incorrectly";
	}	
}

/* 
 * Envelope :from 
 */

test_set "message" text:
From: stephan@rename-it.nl
To: nico@vestingbar.nl
Subject: Frop!

Klutsefluts.
.
;

test_set "envelope.from" "sirius@rename-it.nl";
test_set "envelope.to" "bertus@vestingbar.nl";

test_result_reset;

test "Envelope :from" {
	notify :from "nico@vestingbar.nl"
		"mailto:stephan@rename-it.nl?cc=timo@example.com";

	if not test_result_execute {
		test_fail "failed to execute notify";
	}

	test_message :smtp 0;

	if not envelope :is "from" "nico@vestingbar.nl" {
		test_fail "envelope sender set incorrectly";
	}

	if not envelope :is "to" "stephan@rename-it.nl" {
		test_fail "envelope sender set incorrectly";
	}

	test_message :smtp 1;

	if not envelope :is "from" "nico@vestingbar.nl" {
		test_fail "envelope sender set incorrectly";
	}

	if not envelope :is "to" "timo@example.com" {
		test_fail "envelope sender set incorrectly";
	}	
}

/* 
 * Envelope <> 
 */

test_set "message" text:
From: stephan@rename-it.nl
To: nico@vestingbar.nl
Subject: Frop!

Klutsefluts.
.
;

test_set "envelope.from" "<>";
test_set "envelope.to" "bertus@vestingbar.nl";

test_result_reset;

test "Envelope <>" {
	notify :from "nico@vestingbar.nl"
		"mailto:stephan@rename-it.nl?cc=timo@example.com";

	if not test_result_execute {
		test_fail "failed to execute notify";
	}

	test_message :smtp 0;

	if not envelope :is "from" "" {
		test_fail "envelope sender set incorrectly";
	}

	if not envelope :is "to" "stephan@rename-it.nl" {
		test_fail "envelope sender set incorrectly";
	}

	test_message :smtp 1;

	if not envelope :is "from" "" {
		test_fail "envelope sender set incorrectly";
	}

	if not envelope :is "to" "timo@example.com" {
		test_fail "envelope sender set incorrectly";
	}	
}
