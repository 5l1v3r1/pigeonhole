require "vnd.dovecot.testsuite";
require "envelope";

test_set "message" text:
From: stephan@rename-it.nl
To: tss@iki.fi
Subject: Frop!

Frop!
.
;

test_set "envelope.from" "sirius@rename-it.nl";
test_set "envelope.to" "timo@iki.fi";

test "Redirect" {
	redirect "cras@iki.fi";

	if not test_result_execute {
        test_fail "failed to execute redirect";
    }

    test_message :smtp 0;

    if not address :is "to" "tss@iki.fi" {
        test_fail "to address incorrect (strange forward)";
    }

    if not address :is "from" "stephan@rename-it.nl" {
        test_fail "from address incorrect (strange forward)";
    }

	if not envelope :is "to" "cras@iki.fi" {
		test_fail "envelope recipient incorrect";
	}

	if not envelope :is "from" "sirius@rename-it.nl" {
		test_fail "envelope sender incorrect";
	}
}

test_result_reset;

test "Redirect from <>" {

	test_set "envelope.from" "<>";

	redirect "cras@iki.fi";

	if not test_result_execute {
        test_fail "failed to execute redirect";
    }

	if envelope :is "from" "sirius@rename-it.nl" {
        test_fail "envelope sender incorrect (not changed)";
    }

	if not envelope :is "from" "" {
        test_fail "envelope sender incorrect";
    }
}
