require "vnd.dovecot.testsuite";

test_set "message" text:
From: stephan@rename-it.nl
Message-ID: <frop33333333333333333@frutsens.nl>
To: nico@vestingbar.nl
Subject: Frop.

Friep.
.
;

test "Graceful Conflicts" {
	if not allof (
		test_script_compile "fileinto-inbox.sieve",
		test_script_run ){
		test_fail "failed to compile and run first script";
	}

	if not test_result_execute {
		test_fail "result execute failed after first script";
	}

	if not allof ( 
		test_script_compile "reject-1.sieve",
		test_script_run :append_result ) {
		test_fail "failed to compile and run second script";
	}

	if not test_result_execute {
		test_fail "result execute failed after second script";
	}

	if not allof ( 
		test_script_compile "reject-2.sieve",
		test_script_run :append_result ) {
		test_fail "failed to compile and run third script";
	}

	if not test_result_execute {
		test_fail "result execute failed after third script";
	}

	if not test_result :index 1 "store" {
		test_fail "first action is not 'store'";
	}

	if test_result :index 2 "reject" {
		test_fail "reject action not discarded";
	}
}
